PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>

CONSTRUCT {
  ?concept a skos:Concept;
      skos:prefLabel ?label;
      rdfs:comment ?desc;
      skos:inScheme <https://data.meemoo.be/terms/ond/vak>;
      skos:related ?thema;
      skos:related ?graad.
  
  <https://data.meemoo.be/terms/ond/vak> a skos:ConceptScheme;
    dcterms:title "Meemoo lijst vakken"@nl.

}
FROM <file:vak.csv#encoding=utf-8;header=absent>
WHERE {
    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?b, " ", "-"))) AS ?key)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/vak#', ?key)) AS ?concept)
    BIND (STRLANG(?b, "nl") AS ?label)
    BIND (STRLANG(?c, "nl") AS ?desc)

    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?e, " ", "-"))) AS ?vak_graad)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/graad#', ?vak_graad)) AS ?graad)

    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?f, " ", "-"))) AS ?vak_thema)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/thema#', ?vak_thema)) AS ?thema)
}
OFFSET 1
