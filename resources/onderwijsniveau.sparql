PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ond: <https://data.hetarchief.be/id/onderwijs/>
PREFIX meemoo: <https://data.hetarchief.be/ns/onderwijs#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?concept a skos:Concept;
      skos:prefLabel ?label;
      skos:definition ?desc;
      skos:topConceptOf ond:structuur;
      skos:inScheme ond:structuur;
      skos:narrower ?graad.

  ?graad skos:broader ?concept.

  ond:niveau a skos:Collection;
    skos:member ?concept;
    rdfs:label "Onderwijsniveaus"@nl.

  ond:structuur a skos:ConceptScheme;
    skos:hasTopConcept ?concept;
    rdfs:label "Meemoo lijst onderwijsstructuur"@nl.
}
WHERE {
    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?b, " ", "-"))) AS ?key)
    BIND (URI(CONCAT('https://data.hetarchief.be/id/onderwijs/niveau/', ?key)) AS ?concept)
    BIND (STRLANG(?b, "nl") AS ?label)
    BIND (STRLANG(?c, "nl") AS ?desc)

    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?d, " ", "-"))) AS ?graad_key)
    BIND (URI(CONCAT('https://data.hetarchief.be/id/onderwijs/graad/', ?graad_key)) AS ?graad)

}
OFFSET 1
