PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ond: <https://data.meemoo.be/terms/ond/>

CONSTRUCT {
  ?concept a skos:Concept;
      skos:prefLabel ?label;
      skos:definition ?desc;
      skos:topConceptOf ond:themas;
      skos:inScheme ond:themas;
      skos:related ?vak.

  ond:themas a skos:ConceptScheme;
    skos:hasTopConcept ?concept;
    rdfs:label "Meemoo lijst themas"@nl.
}
WHERE {
    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?b, " ", "-"))) AS ?key)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/thema#', ?key)) AS ?concept)
    BIND (STRLANG(?b, "nl") AS ?label)
    BIND (STRLANG(?c, "nl") AS ?desc)

    BIND (ENCODE_FOR_URI(LCASE(REPLACE(
            IF(  
                CONTAINS(?d, " ("),
                STRBEFORE(?d," ("),
                ?d
            )
        , " ", "-"))) AS ?vak_key)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/vak#', ?vak_key)) AS ?vak)
}
OFFSET 1
