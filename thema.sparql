PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ond: <https://data.meemoo.be/terms/ond/>

CONSTRUCT {
  ?concept a skos:Concept;
      skos:prefLabel ?label;
      skos:definition ?desc;
      skos:topConceptOf ond:themas;
      skos:inScheme ond:themas;
      skos:related ?vak.

  ond:themas a skos:ConceptScheme;
    skos:hasTopConcept ?concept;
    dcterms:title "Meemoo lijst themas"@nl.
}
FROM <file:dist/thema.csv#encoding=utf-8;header=absent>
WHERE {
    BIND (ENCODE_FOR_URI(LCASE(REPLACE(?b, " ", "-"))) AS ?key)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/thema#', ?key)) AS ?concept)
    BIND (STRLANG(?b, "nl") AS ?label)
    BIND (STRLANG(?c, "nl") AS ?desc)

    BIND (ENCODE_FOR_URI(LCASE(REPLACE(STRBEFORE(?d," ("), " ", "-"))) AS ?vak_key)
    BIND (URI(CONCAT('https://data.meemoo.be/terms/ond/vak#', ?vak_key)) AS ?vak)
}
OFFSET 1
